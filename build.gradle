plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.8'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	runtimeOnly 'com.h2database:h2'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.rest-assured:rest-assured'
	testImplementation 'io.cucumber:cucumber-java:7.14.0'
	testImplementation 'io.cucumber:cucumber-junit-platform-engine:7.14.0'
	testImplementation 'io.cucumber:cucumber-spring:7.14.0'
	testImplementation 'org.junit.platform:junit-platform-suite'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'org.testcontainers:postgresql'
	runtimeOnly 'org.postgresql:postgresql'
}

tasks.named('test') {
	useJUnitPlatform()
	exclude 'giftdocker/**'
}

tasks.register('cucumberTest', Test) {
	testClassesDirs = sourceSets.test.output.classesDirs
	classpath = sourceSets.test.runtimeClasspath
	useJUnitPlatform()
	include 'giftdocker/CucumberDockerTest.class'
	systemProperty 'spring.profiles.active', 'cucumber'
}

tasks.register('cucumberTestAlone', Test) {
	testClassesDirs = sourceSets.test.output.classesDirs
	classpath = sourceSets.test.runtimeClasspath
	useJUnitPlatform()
	include 'gift/CucumberTest.class'
	systemProperty 'spring.profiles.active', 'cucumber-alone'
}

tasks.register('dockerBuild', Exec) {
	dependsOn bootJar
	commandLine '/usr/local/bin/docker', 'build', '-t', 'gift-app:latest', '.'
}

tasks.register('dockerUp', Exec) {
	commandLine '/usr/local/bin/docker', 'compose', 'up', '-d', '--wait'
}

tasks.register('dockerDown', Exec) {
	commandLine '/usr/local/bin/docker', 'compose', 'down'
}
